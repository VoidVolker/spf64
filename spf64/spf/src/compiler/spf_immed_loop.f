( Слова немедленного выполнения, используемые при компиляции
  циклов в теле высокоуровневого определения.
  ОС-независимые определения.
  Copyright [C] 1992-1999 A.Cherezov ac@forth.org
  Преобразование из 16-разрядного в 32-разрядный код - 1995-96гг
  Ревизия - сентябрь 1999
)


: _DO   \ 94
\ Интерпретация: семантика неопределена.
\ Компиляция: ( C: -- do-sys )
\ Положить do-sys на стек управления. Добавить семантику времени выполнения, 
\ данную ниже, к текущему определению. Семантика незавершена до разрешения
\ потребителем do-sys, таким как LOOP.
\ Время выполнения: ( n1|u1 n2|u2 -- ) ( R: -- loop-sys )
\ Установить параметры цикла на индекс n2|u2 и предел n1|u1. Неопределенная 
\ ситуация возникает, если n1|u1 и n2|u2 не одного типа. Все, что уже 
\ находилось на стеке возвратов, становится недоступным до тех пор, пока не 
\ будут убраны параметры цикла.
 <'> XDO _COMPILE, 
   0x68 C, DP A@ 4 ALLOT
   0x52 C,    \ PUSH EDX
   0x53 C,    \ PUSH EBX
  4 ALIGN-NOP
  DP A@ \ DUP TO :-SET
; IMMEDIATE

: ?DO   \ 94 CORE EXT
\ Интерпретация: семантика неопределена.
\ Компиляция: ( C: -- do-sys )
\ Положить do-sys на стек управления. Добавить семантику времени выполнения, 
\ данную ниже, к текущему определению. Семантика незавершена до разрешения
\ потребителем do-sys, таким как LOOP.
\ Время выполнения: ( n1|u1 n2|u2 -- ) ( R: --  | loop-sys )
\ Если n1|u1 равно n2|u2, продолжить выполнение с места, данного потребителем 
\ do-sys. Иначе установить параметры цикла на индекс n2|u2 и предел n1|u1
\ и продолжить выполнение сразу за ?DO. Неопределенная 
\ ситуация возникает, если n1|u1 и n2|u2 не одного типа. Все, что уже 
\ находилось на стеке возвратов, становится недоступным до тех пор, пока не 
\ будут убраны параметры цикла.
  ?COMP 
\  OP0 @ :-SET  UMAX TO :-SET
\  'NIPNIP INLINE,

  0x48 C, 0x8D C, 0x6D C, 16 C,  \  lea    0x8*2(%rbp),%rbp
  0xBB C, HERE 4 ALLOT			\  mov        ,%ebx
  
  0x3B C, 0x45 C, -16 C,       \  cmp    -0x8(%rbp),%eax  
\ jne DOTSTTT+0x12 
  0x75 C, 0x5 C,				\ jne
  0x8B C, 0x45 C, -8 C,       \ mov    -0x4(%rbp),%eax
  0xFF C, 0xE3 C,				\ jmpq   *%rbx
  0x53 C,						\ push   %rbx
  0xBB C,  0x80000000 L,         \ mov    $0x80000000,%ebx 
  0x2B C, 0x5D C, -16 C,       \ sub    -0x8(%rbp),%ebx
  0x53 C,						\ push   %rbx
  0x3 C, 0xD8 C, 				\ add    %eax,%ebx
  0x53 C,						\ push   %rbx
  0x48 C, 0x8B C, 0x45 C, -8 C, 		\ mov    -8(%rbp),%rax
  

 \ 'C-?DO INLINE,
  DP A@ \ DUP TO :-SET
; IMMEDIATE


: _LOOP   \ 94
\ Интерпретация: ( C: do-sys -- )
\ Добавить семантику времени выполнения, данную ниже, к текущему определению.
\ Разрешить все появления LEAVE между позицией, данной do-sys и следующей
\ позицией передачи управления для выполнения слов за LOOP.
\ Время выполнения: ( -- ) ( R: loop-sys1 --  | loop-sys2 )
\ Неопределенная ситуация возникает, если параметры цикла недоступны.
\ Прибавить единицу к индексу цикла. Если индекс цикла стал равным пределу, 
\ убрать параметры цикла и продолжить выполнение сразу за циклом. Иначе 
\ продолжить выполнение с начала цикла.
  ?COMP 
  0x24 0x4FF W, C, \ inc dword [esp]
  HERE 2+ -  DUP SHORT? \  SetOP SetJP
  IF
    0x71 C, C, \ jno short 
  ELSE
    4 - 0xF C, 0x81 C, L, \ jno near
  THEN  \ SetOP
  0x48 C, 0x1824648D L, \ lea 0x18(%rsp),%rsp
  DP A@ SWAP L!
; IMMEDIATE

: +LOOP    \ 94
\ Интерпретация: ( C: do-sys -- )
\ Добавить семантику времени выполнения, данную ниже, к текущему определению.
\ Разрешить все появления LEAVE между позицией, данной do-sys и следующей
\ позицией передачи управления для выполнения слов за LOOP.
\ Время выполнения: ( n -- ) ( R: loop-sys1 --  | loop-sys2 )
\ Неопределенная ситуация возникает, если параметры цикла недоступны.
\ Прибавить n к индексу цикла. Если индекс цикла не пересек границу между
\ пределом цикла минус единица и пределом цикла, продолжить выполнение с
\ начала цикла. Иначе убрать параметры цикла и продолжить выполнение сразу
\ за циклом.
   ?COMP  
   <'> ADD[ESP],TOS  _COMPILE,
\   ['] DROP _COMPILE,
   HERE 2+  - 
   DUP  SHORT? \  SetOP SetJP
  IF    0x71 C, C, \ jno short 
  ELSE    4 - 0xF C, 0x81 C, L, \ jno near
  THEN    \  SetOP
   0x48 C, 0x1824648D L, \ lea esp, 0c [esp]
  DP A@ SWAP   L!
; IMMEDIATE


: LEAVE    \ 94
\ Интерпретация: семантика неопределена.
\ Выполнение: ( -- ) ( R: loop-sys -- )
\ Убрать текущие параметры цикла. Неопределенная ситуация возникает, если 
\ они недоступны. Продолжить выполнение сразу за самыми внутренними DO ... LOOP 
\ или DO ... +LOOP.
  ?COMP
  0x48 C, 0x1024648D L, \ lea esp, 0x10 [esp]
  0xC3 C,  \ ret
; IMMEDIATE

: UNLOOP  \ 94
\ Интерпретация: семантика неопределена.
\ Выполнение: ( -- ) ( R: loop-sys -- )
\ Убрать параметры цикла текущего уровня. UNLOOP требуется для каждого
\ уровня вложения циклов перед выходом из определения по EXIT.
\ Неоднозначная ситуация возникает, если параметры цикла недоступны.
  ?COMP
  0x48 C, 0x1824648D L, \ lea 0x18(%rsp),%rsp
; IMMEDIATE

DECIMAL
